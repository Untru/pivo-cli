#Использовать v8rac
&Пластилин Перем МенеджерОпций; //Класс для хранения опций выполнения

Перем Кластер; //Класс v8rac УправлениеКластером
Перем ИдентификаторИнформационнойбаза;
Перем Инициализирован; //Инициализирован ли кластер

&Логгер
Перем Логгер;

&Желудь
Процедура ПриСозданииОбъекта() Экспорт
	Инициализирован = Ложь;
КонецПроцедуры

Процедура Инициализировать() Экспорт
	
	Если Не ЗначениеЗаполнено(МенеджерОпций.ВернутьСвойство("NameDB")) Тогда
		Логгер.Ошибка("Имя информационной базы не задано для работы с кластером");
		Возврат;
	КонецЕсли;

	Кластер = Новый УправлениеКластером;

	ПутьКСерверу = МенеджерОпций.ВернутьСвойство("PathServerRAS");
	Если Не ЗначениеЗаполнено(ПутьКСерверу) Тогда
		ПутьКСерверу = МенеджерОпций.ВернутьСвойство("PathServer1с");
	КонецЕсли;
	
	Кластер.УстановитьКластер(ПутьКСерверу);
	Кластер.ИспользоватьВерсию("8.3");
	Попытка
		Кластер.Подключить();		
	Исключение
		Логгер.Ошибка("Не удалось подключиться к кластеру");
		Возврат;
	КонецПопытки;

	ИдентификаторИнформационнойбаза = Кластер.НайтиИнформационнуюБазу(МенеджерОпций.ВернутьСвойство("NameDB"));
	
	Кластер.УстановитьАвторизациюИнформационнойБазы(ИдентификаторИнформационнойбаза
		, МенеджерОпций.ВернутьСвойство("UsrLogin1c"), МенеджерОпций.ВернутьСвойство("UsrPswd1c"));
	
	Инициализирован = Истина;
	
КонецПроцедуры


// Устанавливает авторизацию на кластере 1С
//
// Параметры:
//   Пользователь - Строка - администратор кластера
//   Пароль - Строка - пароль администратора кластера
//
Процедура УстановитьАвторизациюКластера(Знач Пользователь, Знач Пароль = "") Экспорт
	
	Кластер.УстановитьАвторизациюКластера(Пользователь, Пароль);

КонецПроцедуры

Процедура ЗаблокироватьБазу(ОтключатьСеансы = Истина) Экспорт
	
	Если Не Инициализирован Тогда
		Логгер.Информация("Кластер не инициализирован для работы с блокировкой базы");
		Возврат;
	КонецЕсли;

	ДатаОкончанияБлокировки = ТекущаяДата() + 60 * 60 * 2; //2 часа
	КодДоступа = МенеджерОпций.ВернутьСвойство("AccessCode");
	Если Не ЗначениеЗаполнено(КодДоступа) Тогда
		КодДоступа = "Обновление";
	КонецЕсли;
	Кластер.БлокировкаИнформационнойБазы(ИдентификаторИнформационнойбаза, 
		"База заблокирована для обновления", КодДоступа,, ДатаОкончанияБлокировки
	);

	Если ОтключатьСеансы Тогда
		Кластер.ОтключитьСеансыИнформационнойБазы(МенеджерОпций.ВернутьСвойство("NameDB"));
		Кластер.ОтключитьСоединенияИнформационнойБазы(МенеджерОпций.ВернутьСвойство("NameDB"));
	КонецЕсли;
	
КонецПроцедуры

Процедура РазблокироватьБазу() Экспорт
	
	Если Не Инициализирован Тогда
		Логгер.Информация("Кластер не инициализирован для работы с блокировкой базы");
		Возврат;
	КонецЕсли;

	Логгер.Информация(ИдентификаторИнформационнойбаза);
	Кластер.СнятьБлокировкуИнформационнойБазы(ИдентификаторИнформационнойбаза, Истина);
	
КонецПроцедуры